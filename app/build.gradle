buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


ext.versions = [
        // Android
        targetSdkVersion    : 27,
        minSdkVersion       : 15,
        buildtools          : "27.0.3",
        support             : "27.1.0",
        retrofit            : "2.3.0",
        room                : "1.0.0"
]


def versionMajor = 0
def versionMinor = 5
def versionBuild = 29

android {
    compileSdkVersion versions.targetSdkVersion
    buildToolsVersion versions.buildtools
    defaultConfig {
        applicationId "doit.study.droid"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support:preference-v7:$versions.support"
    implementation "com.android.support:cardview-v7:$versions.support"
    implementation "com.android.support:recyclerview-v7:$versions.support"
    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:support-v4:$versions.support"

    compile "android.arch.persistence.room:runtime:$versions.room"
    compile "android.arch.persistence.room:rxjava2:$versions.room"
    annotationProcessor "android.arch.persistence.room:compiler:$versions.room"

    // Retrofit & OkHttp
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation 'com.google.android.gms:play-services-analytics:11.8.0'

    implementation 'com.jakewharton.timber:timber:4.6.1'
    // in terminal run adb forward tcp:8080 tcp:8080 than open http://localhost:8080/
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    implementation 'com.mikhaellopez:circularprogressbar:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation 'com.android.support:multidex:1.0.3'

}


task exportQuestions(type: Exec, description: 'Run the pre-build python script') {
    workingDir 'src/main/res/raw/'
    commandLine 'python', 'import_quiz_to_json.py'
}

// This ensures my script will be called before building
// preBuild.dependsOn exportQuestions
